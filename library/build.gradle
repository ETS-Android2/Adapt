apply plugin: 'com.android.library'

android {

    compileSdkVersion config['target-sdk']
    buildToolsVersion config['build-tools']

    defaultConfig {
        minSdkVersion config['min-sdk']
        targetSdkVersion config['target-sdk']
        versionCode 1
        versionName version
    }

    sourceSets {
        main.java.srcDirs += './build/src-gen/'
    }
}

dependencies {

    deps.with {
        api it['support-annotations']
        api it['recycler-view']
    }

    deps['test'].with {
        testImplementation it['junit']
        testImplementation it['mockito']
        testImplementation it['robolectric']
    }
}

if (project.hasProperty('release')) {
    apply from: config['push-aar-gradle']
}

task generateBuildConfig {
    final def folder = file('build/src-gen/ru/noties/adapt/')
    folder.mkdirs()
    new File(folder, 'AdaptBuildConfig.java').withWriter("UTF-8", {
        it.write("""
package ru.noties.adapt;

abstract class AdaptBuildConfig {

  static final String VERSION_NAME = \"$version\";

  private AdaptBuildConfig() {
  }
}
""")
    })
}

afterEvaluate {
    generateReleaseBuildConfig.enabled = false
    generateDebugBuildConfig.enabled = false

    tasks.withType(JavaCompile.class).all {
        it.dependsOn generateBuildConfig
    }
}