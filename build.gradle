buildscript {
    ext.kotlin_version = '1.4.10'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        if (project.hasProperty('LOCAL_MAVEN_URL')) {
            maven { url LOCAL_MAVEN_URL }
        }
        google()
        jcenter()
    }
    group = GROUP
    version = VERSION_NAME

    tasks.withType(Javadoc) {
        enabled(false)
    }
}

if (hasProperty('local')) {
    if (!hasProperty('LOCAL_MAVEN_URL')) {
        throw new RuntimeException('Cannot publish to local maven as no such property exists: LOCAL_MAVEN_URL')
    }
    ext.RELEASE_REPOSITORY_URL = LOCAL_MAVEN_URL
    ext.SNAPSHOT_REPOSITORY_URL = LOCAL_MAVEN_URL
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

wrapper {
    gradleVersion '6.1.1'
    distributionType 'all'
}

ext {

    config = [
            'min-sdk'        : 21,
            'target-sdk'     : 30,
            'build-tools'    : '30.0.2',
            'push-aar-gradle': 'https://raw.githubusercontent.com/noties/gradle-mvn-push/master/gradle-mvn-push-aar.gradle'
    ]

    deps = [
            'x-annotations'  : 'androidx.annotation:annotation:1.1.0',
            'x-recycler-view': 'androidx.recyclerview:recyclerview:1.0.0',
            'kotlin-stdlib'  : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    ]

    deps['test'] = [
            'junit'      : 'junit:junit:4.12',
            'mockito'    : 'org.mockito:mockito-core:2.18.3',
            'robolectric': 'org.robolectric:robolectric:3.8'
    ]
}
