buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
    }
}

allprojects {
    repositories {
        if (project.hasProperty('LOCAL_MAVEN_URL')) {
            maven { url LOCAL_MAVEN_URL }
        }
        google()
        jcenter()
    }
    group = GROUP
    version = VERSION_NAME

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
        options.addStringOption('charSet', 'UTF-8')
    }
}

if (hasProperty('local')) {
    if (!hasProperty('LOCAL_MAVEN_URL')) {
        throw new RuntimeException('Cannot publish to local maven as no such property exists: LOCAL_MAVEN_URL')
    }
    ext.RELEASE_REPOSITORY_URL = LOCAL_MAVEN_URL
    ext.SNAPSHOT_REPOSITORY_URL = LOCAL_MAVEN_URL
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

wrapper {
    gradleVersion '4.10.2'
    distributionType 'all'
}

ext {

    config = [
            'min-sdk'        : 16,
            'target-sdk'     : 28,
            'build-tools'    : '28.0.3',
            'push-aar-gradle': 'https://raw.githubusercontent.com/noties/gradle-mvn-push/master/gradle-mvn-push-aar.gradle'
    ]

    final def supportVersion = '28.0.0'

    deps = [
            'support-annotations': "com.android.support:support-annotations:$supportVersion",
            'recycler-view'      : "com.android.support:recyclerview-v7:$supportVersion"
    ]

    deps['test'] = [
            'junit'      : 'junit:junit:4.12',
            'mockito'    : 'org.mockito:mockito-core:2.18.3',
            'robolectric': 'org.robolectric:robolectric:3.8'
    ]
}
